// BROT - A fast mandelbrot set explorer
// Copyright (C) 2025  Charles Reischer
// 
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published
// by the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Affero General Public License for more details.
// 
// You should have received a copy of the GNU Affero General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

#version 450

layout(set = 0, std430, binding = 0) readonly buffer buffer_src {
    float buff_potential_vals_src[];
};

layout(set = 1, std430, binding = 0) writeonly buffer buffer_dst {
    float buff_potential_vals_dst[];
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout( push_constant ) uniform UniformBufferObject {
    uvec2 dst_pos_offset;
    uvec2 src_offset;
    uvec2 buffer_size;
    int scale_diff;
} ubo;

ivec2 shift(ivec2 vec, int shft) {
    int x, y;
    if (shft < 0) { // multiple dst pixel read from same src, i.e. scale up
        x = vec.x >> -shft;
        y = vec.y >> -shft;
    } else { // src pixels are skiped, i.e. scale down
        x = vec.x << shft;
        y = vec.y << shft;
    }
    return ivec2(x, y);
}

void main() {
    ivec2 true_loc = ivec2(gl_GlobalInvocationID.xy) - ivec2(ubo.dst_pos_offset);
    ivec2 src_loc = ivec2(ubo.src_offset) + shift(true_loc, ubo.scale_diff);
    uvec2 dst_loc = gl_GlobalInvocationID.xy;

    float potential;
    if (src_loc.x < 0 || src_loc.x >= ubo.buffer_size.x || src_loc.y < 0 || src_loc.y >= ubo.buffer_size.y) {
        potential = -1.0;
    } else {
        potential = buff_potential_vals_src[src_loc.x + src_loc.y * ubo.buffer_size.x];
    }

    buff_potential_vals_dst[dst_loc.x + dst_loc.y * ubo.buffer_size.x] = potential;
}
